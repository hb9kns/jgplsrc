NB. numeric constants ---------------------------------------------------

3j4      -: ". '3j4'
1 _2 3j4 -: ". '1 _2 3j4'

0.34     -: ". '0.34'
3j4 0.34 -: ". '3j4 0.34'

X -: ". ": X=:?3 4$10000
X -: ". ": X=:_1e4+?3 4$2e4
X -: ". ": X=:j./?2 3 4$10000
X -: ". ": X=:j./_1e4+?2 3 4$2e4

X -: ". ":     ,.X=:  _1e9+?20$2e9
X -: ". ":!.16 ,.X=:o._1e9+?20$2e9
X -: ". ":!.16 ,.X=:j./  _1e9+?2 20$2e9

(^1 2)        -: ". '1x1 1x2 '
((^1),1+%:_1) -: ". '1x1 1j1 '
((^1),o.1)    -: ". '1x1 1p1 '

((o.1)^1 2)   -: ". '1p1 1p2'

2b111 5b312   -: ". '2b111 5b312 '
2b111 5b312   -: ". '2b111 5b312 '

0j4 _5        -: ". '4ad90 5ar3.14159265358979'

((%3),_0.25)  -: ". '    1r3 _1r4'

eq=: -:&(3!:1)
1 2 _  3.4 eq ". '1 2 _  3.4'
1 2 _  3j4 eq ". '1 2 _  3j4'
1 2 __ 3.4 eq ". '1 2 __ 3.4'
1 2 __ 3j4 eq ". '1 2 __ 3j4'
1 2 _. 3.4 eq ". '1 2 _. 3.4'
1 2 _. 3j4 eq ". '1 2 _. 3j4'

'_. 3'       -: ": _. 3
'_. 3'       -: ": _. 3.0
'_. 3'       -: ": _. 3j0
'_. 3.14159' -: ": _. 1p1
'_. 2.71828' -: ": _. 1x1

X=: 1e4 ?@$ IF64{2e9 9e18
X    eq ". ":  X
(-X) eq ". ": -X

(4=type X) *. 0 = (- Y) - X=:  ". '_',":    Y=: 2^IF64{31 63x
(4=type X) *. 0 = (<:Y) - X=:  ".     ": <: Y=: 2^IF64{31 63x

'ill-formed number' -: ". etx '3ee4'
'ill-formed number' -: ". etx '3jj4'


4!:55 ;:'eq X Y'


