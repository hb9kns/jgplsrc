NB. e.Y -----------------------------------------------------------------

Y =. 'abc';'dc';'a'
(e.Y) -: 1 1 1 0 1 1,0 0 1 1 1 0,:1 0 0 0 0 1

X =. 1 0 0 1 0 1 (<;.1) 3#"1,.'abcdca'
(e.X) -: e.Y

(e.'') -: i.0 0

'domain error' -: e. etx 1 2;'abc'  
'domain error' -: e. etx 1 2;<<'abc' 
'domain error' -: e. etx 'ab';<<'a' 


NB. X e.Y ---------------------------------------------------------------

1 0 0 -: 'foo' e.'f'
0 0 0 -: 'foo' e.'X'
0 0 0 -: 'foo' e.4
1 0 0 -: 1 0 0 e.1
1 0 0 -: 2 1 4 e.2
1 0 0 -: (o.3 1 2)e.o.3

0 0     -: (i.2 3) e. 4 3$'a'
0 0 0   -: (i.3 2 4) e. 1 2 4$<'a'
(3 2$0) -: (3 2 4$'a') e. i.2 4

nan=: e.!.0&_.

0 0 0 0 -: nan 0 1 2  3
0 0 0 0 -: nan 0 1 2  3
0 0 1 0 -: nan 0 1 _. 3
0 0 1 0 -: nan 0 1 _. 3

test=: 3 : 0
 yy=: y
 i=: ?101$#yy
 b=: yy e. i{yy
 assert. 1=type b
 assert. 1=#$b
 assert. b -:&# yy
 assert. b >: 1 i}(#yy)$0
 1
)

test@:(t"_ {~ 877&, ?@$ (#t)"_)"0 >:i.3 5 [ t=: 0 1
test@:(t"_ {~ 877&, ?@$ (#t)"_)"0 >:i.3 5 [ t=: 'abcde'
test@:(t"_ {~ 877&, ?@$ (#t)"_)"0 >:i.3 5 [ t=: ?5$20
test@:(t"_ {~ 877&, ?@$ (#t)"_)"0 >:i.3 5 [ t=: ?5$2e9
test@:(t"_ {~ 877&, ?@$ (#t)"_)"0 >:i.5   [ t=: o.?5$20
test@:(t"_ {~ 877&, ?@$ (#t)"_)"0 >:i.5   [ t=: j./?2 5$20
test@:(t"_ {~ 877&, ?@$ (#t)"_)"0 >:i.5   [ t=: ;:' miasma chthonic chronic kakistocracy dado'
test@:(t"_ {~ 877&, ?@$ (#t)"_)"0 >:i.5   [ t=: x: ?5$20
test@:(t"_ {~ 877&, ?@$ (#t)"_)"0 >:i.5   [ t=: %/0 1+x:?2 5$20
test@:(t"_ {~ 877&, ?@$ (#t)"_)"0 >:i.3 5 [ t=: u: ?5$65536
test@:(t"_ {~ 877&, ?@$ (#t)"_)"0 >:i.3 5 [ t=: s:' miasma chthonic chronic kakistocracy dado'

0 0 -: (i.2 3) e. i.2 4
0 0 -: (i.2 3) e. 4 5$'a'

0   -: 3 e. i.4 3


NB. X e."r Y ------------------------------------------------------------

g=: 4 : 'x e. y'

X=.?2 3 4 5$5
X (g"0 0 -: e."0 0) Y=.?2 3 4 5$5
X (g"1 0 -: e."1 0) Y=.?2 3 4  $5
X (g"2 0 -: e."2 0) Y=.?2 3    $5 
X (g"3 0 -: e."3 0) Y=.?2      $5
X (g"4 0 -: e."4 0) Y=.?        5
X (g"0 1 -: e."0 1) Y=.?13     $5
X (g"1 1 -: e."1 1) Y=.?2 3 4 7$5
X (g"2 1 -: e."2 1) Y=.?2 3   2$5
X (g"3 1 -: e."3 1) Y=.?2     7$5
X (g"4 1 -: e."4 1) Y=.?      9$5
X (g"1 2 -: e."1 2) Y=.?2 3 4 2 5$5
X (g"2 2 -: e."2 2) Y=.?2 3   1 5$5
X (g"3 2 -: e."3 2) Y=.?2     4 5$5
X (g"4 2 -: e."4 2) Y=.?3    17 5$5
X (g"2 3 -: e."2 3) Y=.?2 3 7 4 5$5
X (g"3 3 -: e."3 3) Y=.?2   7 4 5$5
X (g"4 3 -: e."4 3) Y=.?11  7 4 5$5

(i.0 3 4) (g"1 -: e."1) i.0 3 7


4!:55 ;:'b g i nan t test X Y yy'


